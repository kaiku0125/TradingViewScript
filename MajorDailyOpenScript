//@version=5

import kaiku0125/Debug/1 as debug
import kaiku0125/Colour/1 as colour
//日線陰陽轉換條件
//1.陰陽轉換
//2.區域高低點
//3.成交量要大
//4.

// # ========================================================================= #
// #                   |   Indicator  |
// # ========================================================================= #
indicator(
 title                = "MajorDailyOpenFinder",
 shorttitle           = "MDOFinder",
 overlay              =  true,
//  format               =  ,
//  precision            =  ,
//  scale                =  ,
//   timeframe            = "D",
//  timeframe_gaps       =  true
//  explicit_plot_zorder =  ,
 max_lines_count      =  6,
 max_labels_count     =  500
//  max_boxes_count      =  
 )
// # ========================================================================= #
// #                   |   Indicator  |
// # ========================================================================= #


//variable
//time series(Day = 1440 min, so 6hr offset = 1440 / 360)
var string daily = "D"
var int daily_offset = 0

var string hour_6 = "360"
var int hour_6_offset = 4
var string hour_4 = "240"
var int hour_4_offset = 6

var string hour_3 = "180"
var int hour_3_offset = 8
var string hour_2 = "120"
var int hour_2_offset = 12

var string hour_1 = "60"
var int hour_1_offset = 24

var bool isDEBUG = false
var int BEAR = 0
var int BULL = 1
var bool isBarChanged = false
var bool nextBarNeedShow = false
var int lastDayState = BULL
var int currentDayState = BULL

var label debugLabel = na

//垂直柱
var float percent = 0.0
var int percentIndex = 0

//輸出圖表
var float[] storedOpenPrice = array.new_float()
var float[] displayOpenPrice = array.new_float()
var int[] storedBarIndex = array.new_int() 
var int[] displayBarIndex = array.new_int() 
var float[] storedPercent = array.new_float() 
var float[] displayPercent = array.new_float()
var label displayLabel = na
var line displayLine = na

//function
isBullBar(_close, _open) =>
    bool isBullBar = false
    if _close >= _open
        isBullBar := true
    
    isBullBar

getTimeFrameOffset() =>
    int offset = switch timeframe.period
        daily => daily_offset
        hour_6 => hour_6_offset
        hour_4 => hour_4_offset
        hour_3 => hour_3_offset
        hour_2 => hour_2_offset
        hour_1 => hour_1_offset
        => 
            hour_4_offset
    offset
isCurrentTimeFrameAvailable() =>
    bool isAvailable = switch timeframe.period
        daily => true
        hour_6 => true
        hour_4 => true
        hour_3 => true
        hour_2 => true
        hour_1 => true
        => 
            false

    isAvailable

getBarPercentage(_close, _open) =>
    float returnPercent = na
    returnPercent := ((_close - _open) / _open) * 100

    returnPercent

updateArray(price, priceArray, barIndex, barIndexArray, percent, percentArray) =>
    float[] aFloatPrice = array.copy(id = priceArray) 
    float[] outputPrice = array.new_float() 
    array.push(id = aFloatPrice, value = price) 

    int[] aIntBarIndex = array.copy(id = barIndexArray) 
    int[] outputBarIndex = array.new_int() 
    array.push(id = aIntBarIndex, value = barIndex)

    float[] aFloatPercent = array.copy(id = percentArray) 
    float[] outputPercent = array.new_float()  
    array.push(id = aFloatPercent, value = percent) 

    [aFloatPrice, aIntBarIndex, aFloatPercent]


// Get recently bar and chosen three bull bear bar.
getLatestOpenIndexArray(arrayOpen, arrayIndex, arrayPercent, isBull) =>
    float[] arrayCopyOpenPriceReverse = array.copy(id = arrayOpen)
    array.reverse(id = arrayCopyOpenPriceReverse)
    int[] arrayCopyIndexReverse = array.copy(id = arrayIndex)
    array.reverse(id = arrayCopyIndexReverse) 
    float[] arrayCopyPercentReverse = array.copy(id = arrayPercent)
    array.reverse(id = arrayCopyPercentReverse)
    int[] returnIndexArray = array.new_int(0)

    int barIndexSize = array.size(id = arrayCopyIndexReverse)
    if barIndexSize >= 3
        for i = 0 to (barIndexSize == 0 ? na : array.size(arrayCopyPercentReverse) - 1)
            cond1 = isBull ? array.get(id = arrayCopyPercentReverse, index = i) > 0 : array.get(id = arrayCopyPercentReverse, index = i) < 0 
            cond2 = isBull ? array.get(id = arrayCopyOpenPriceReverse, index = i) - open < 0 : array.get(id = arrayCopyOpenPriceReverse, index = i) - open > 0

            if cond1 and cond2
                array.push(id = returnIndexArray, value = barIndexSize - i - 1)
                if array.size(id = returnIndexArray) == 3
                    break 

    returnIndexArray

choseDisplayParamsOutput() =>
    int[] mBullOutputIndex = getLatestOpenIndexArray(storedOpenPrice, storedBarIndex, storedPercent, true)
    int[] mBearOutputIndex = getLatestOpenIndexArray(storedOpenPrice, storedBarIndex, storedPercent, false)
    array.clear(id = displayOpenPrice) 
    array.clear(id = displayBarIndex) 
    array.clear(id = displayPercent)

    // Get bull output
    for i = 0 to (array.size(mBullOutputIndex) == 0 ? na : array.size(mBullOutputIndex) - 1)
        int targetBullIndex = array.get(id = mBullOutputIndex, index = i)
        array.push(id = displayOpenPrice, value = array.get(id = storedOpenPrice, index =  targetBullIndex) ) 
        array.push(id = displayBarIndex, value = array.get(id = storedBarIndex, index =  targetBullIndex) ) 
        array.push(id = displayPercent, value = array.get(id = storedPercent, index =  targetBullIndex) ) 

    //Get bear output
    for i = 0 to (array.size(mBearOutputIndex) == 0 ? na : array.size(mBearOutputIndex) - 1)
        int targetBearIndex = array.get(id = mBearOutputIndex, index = i)
        array.push(id = displayOpenPrice, value = array.get(id = storedOpenPrice, index =  targetBearIndex) ) 
        array.push(id = displayBarIndex, value = array.get(id = storedBarIndex, index =  targetBearIndex) ) 
        array.push(id = displayPercent, value = array.get(id = storedPercent, index =  targetBearIndex) ) 

//Code login start
//抓出陰陽轉換bar
dayHigh = request.security(syminfo.tickerid, 'D', high)
dayLow = request.security(syminfo.tickerid, 'D', low)
dayOpen = request.security(syminfo.tickerid, 'D', open)
dayClose = request.security(syminfo.tickerid, 'D', close)
dayVolume = request.security(syminfo.tickerid, 'D', volume)
dayBarIndex = request.security(syminfo.tickerid, 'D', bar_index)
dayTimeClose = request.security(syminfo.tickerid, 'D', time_close)


currentDayState := isBullBar(dayClose, dayOpen) ? BULL : BEAR

//日線是否陰陽轉換
cond1 = lastDayState != currentDayState ? true : false

//找出現在時框此bar是否 = 日線開盤的位置
cond2 = if timeframe.period != daily
    dayTimeClose == time_close[1] ? true : false
else 
    true

//日線垂直柱 > 5%漲跌幅
percent := getBarPercentage(dayClose, dayOpen)
// float seriesPercent = math.abs(getBarPercentage(dayClose, dayOpen)) // ta.sma(source = seriesPercent, length = 20) 

cond3 = math.abs(number = percent)  >  5


// 如果此時 上一個顯示要show && 是日線開盤位置 && 漲幅5%以上
bool finalCond = if timeframe.period != daily
    nextBarNeedShow and cond2 and cond3 and isCurrentTimeFrameAvailable()
else
    cond1 and cond2 and cond3 and isCurrentTimeFrameAvailable()

if finalCond
    float thisOpenPrice = open[getTimeFrameOffset()]    
    [array1, array2, array3] = updateArray(thisOpenPrice, storedOpenPrice, bar_index[getTimeFrameOffset()], storedBarIndex, percent, storedPercent)
    storedOpenPrice := array.copy(id = array1)
    storedBarIndex := array.copy(id = array2)
    storedPercent := array.copy(id = array3)


if cond1
    nextBarNeedShow := true
else
    nextBarNeedShow := false
    
lastDayState := currentDayState


if barstate.islast
    choseDisplayParamsOutput()
    // Draw all data on chart
    for i = 0 to (array.size(displayOpenPrice) == 0 ? na : array.size(displayOpenPrice) - 1)
        float tempPrice = array.get(id = displayOpenPrice, index = i)
        string tempPriceText = str.tostring(value = tempPrice) 
        int tempIndex = array.get(id = displayBarIndex, index = i)
        float tempPercent = array.get(id = displayPercent, index = i) 
        displayLabel := label.new(
        //  x            = tempIndex,
         x            = bar_index + getTimeFrameOffset() * 5,
        //  y            = tempPercent > 0 ? low[bar_index - tempIndex]*0.999 : high[bar_index - tempIndex]*1.001,
         y            = tempPrice,
         text         = tempPriceText,
        //  color        = color.new(color = tempPercent > 0 ? color.green : color.red, transp = 0),
        //  style        = tempPercent > 0 ? label.style_label_up : label.style_label_down,
         style        = label.style_none,
        //  textcolor    = color.new(color =  color.white, transp = 0)
         textcolor    = color.new(color = tempPercent > 0 ? color.green : color.red, transp = 0)
         )
        displayLine := line.new(x1 = tempIndex, y1 = tempPrice, x2 = tempIndex + 1, y2 = tempPrice, extend =  extend.right, color = color.new(color =  tempPercent > 0 ? colour.DARK_GREEN() : colour.DARK_RED(), transp = 0), width = 1) 
        
    
    if isDEBUG
        mOutput = str.tostring(value = displayOpenPrice) + "\n"
        mOutput += str.tostring(value = displayBarIndex) + "\n"
        mOutput += str.tostring(value = displayPercent) 
        debugLabel := debug.printLabel(mOutput)
        label.delete(id = debugLabel[1])



